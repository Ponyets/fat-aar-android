apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

Properties properties = new Properties()
File localProperties = rootProject.file('local.properties')
if (localProperties.exists()) {
    localProperties.withInputStream { properties.load(it) }
}

publishing {
    publications {
        release(MavenPublication) {
            // The coordinates of the library, being set from variables that
            // we'll set up in a moment
            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_GROUP_ID + ".gradle.plugin"
            version PUBLISH_VERSION

            artifact jar
            artifact sourcesJar
            artifact javadocJar

            // Self-explanatory metadata for the most part
            pom {
                name = PUBLISH_GROUP_ID + ".gradle.plugin"
                description = 'A gradle plugin that merge dependencies into the final aar file works with AGP 3.+'
                // If your project has a dedicated site, use its URL here
                url = 'https://github.com/kezong/fat-aar-android'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'kezong'
                        name = 'kezong'
                        email = 'kezong1811@gmail.com'
                    }
                }
                // Version control info, if you're using GitHub, follow the format as seen here
                scm {
                    connection = 'scm:git:github.com/kezong/fat-aar-android.git'
                    developerConnection = 'scm:git:ssh://github.com/kezong/fat-aar-android.git'
                    url = 'https://github.com/kezong/fat-aar-android/tree/master'
                }
                // A slightly hacky fix so that your POM will include any transitive dependencies
                // that your library builds upon
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        if (it.group == "com.android.tools.build" && it.name == "gradle") {
                            return
                        }
                        if (it.group == null) {
                            return
                        }
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url PUBLISH_VERSION.endsWith("-SNAPSHOT") ?
                    properties["mavenSnapshotUrl"] : properties["mavenReleaseUrl"]
            allowInsecureProtocol true
            credentials {
                username properties.getProperty("mavenUsername")
                password properties.getProperty("mavenPassword")
            }
        }
    }
}
